#------------------------------------
# o programa sera numa funcao main
#------------------------------------

<function*> -> <type> 'IDENT' '(' <argList> ')' <bloco> ;	OK
<argList> -> <arg> <restoArgList> | & ;						OK
<arg> -> <type> 'IDENT' ;									OK
<restoArgList> -> ',' <argList> | & ;						OK
<type> -> 'int' | 'float' ;									OK
<bloco> -> '{' <stmtList> '}' ;								OK
<stmtList> -> <stmt> <stmtList> | & ;						OK
<stmt> -> <forStmt>											OK
   | <ioStmt>												OK
   | <whileStmt>											OK
   | <expr> ';'												OK
   | <ifStmt>												OK
   | <bloco>												OK
   | 'break'												OK
   | 'continue'												OK
   | <declaration>											OK
   | ';' ;													OK

#---------------------------
# descricao das instrucoes
#---------------------------

# declaracoes
<declaration> -> <type> <identList> ';' ;					OK
<identList> -> 'IDENT' <restoIdentList> ;					OK
<restoIdentList> -> ',' 'IDENT' <restoIdentList> | & ;		OK

# comando for
<forStmt> -> 'for' '(' <optExpr> ';'
              <optExpr> ';' <optExpr> ')' <stmt> ;			OK
<optExpr> -> <expr> | & ;									OK
# comandos de IO
<ioStmt> -> 'scan' '(' 'IDENT' ')' ';'						OK
          | 'print' '(' <outList> ')' ';' ;					OK
<outList> -> <out> <restoOutList> ;							OK
<out> -> 'STR' | 'IDENT' | 'NUMint' | 'NUMfloat' ;			OK
<restoOutList> -> ',' <out> <restoOutList> | & ;			OK
# comando while
<whileStmt> -> 'while' '(' <expr> ')' <stmt> ;				OK
# comando if
<ifStmt> -> 'if' '(' <expr> ')' <stmt> <elsePart> ;			OK
<elsePart> -> 'else' <stmt> | & ;							OK

#------------------------------
# expressoes
#------------------------------
<expr> -> <atrib> ;											OK
<atrib> -> <or> <restoAtrib> ;								OK
<restoAtrib> -> '=' <atrib> | & ;							OK
<or> -> <and> <restoOr> ;									OK
<restoOr> -> '||' <and> <restoOr> | & ;						OK
<and> -> <not> <restoAnd> ;									OK
<restoAnd> -> '&&' <not> <restoAnd> | & ;					OK
<not> -> '!' <not> | <rel> ;								OK
<rel> -> <add> <restoRel> ;									OK
<restoRel> -> '==' <add> | '!=' <add>						OK
            | '<' <add> | '<=' <add>						OK
            | '>' <add> | '>=' <add> | & ;					OK
<add> -> <mult> <restoAdd> ;								OK
<restoAdd> -> '+' <mult> <restoAdd>							OK
            | '-' <mult> <restoAdd> | & ;					OK
<mult> -> <uno> <restoMult> ;								OK
<restoMult> -> '*' <uno> <restoMult>						OK
            |  '/' <uno> <restoMult>						OK
            |  '%' <uno> <restoMult> | & ;					OK
<uno> -> '+' <uno> | '-' <uno> | <fator> ;					OK
<fator> -> 'NUMint' | 'NUMfloat'							OK
         | 'IDENT'  | '(' <atrib> ')' ;						OK

#---------
# the end
#---------
